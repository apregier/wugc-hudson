#!/usr/bin/env perl

use strict;
use warnings;

use Genome;
use Revision;
use Library;
use Users;

my $job_name = $ENV{JOB_NAME} or die 'JOB_NAME not set.';
my $build_url = $ENV{BUILD_URL} or die 'BUILD_URL not set.';
my $build_number = $ENV{BUILD_NUMBER} or die 'BUILD_NUMBER not set.';
my $test_spec = $ENV{TEST_SPEC} or die 'TEST_SPEC not set.';

$0 = sprintf("%s %s # TEST_SPEC = %s", $^X, __FILE__, $test_spec);

print "\n\n => Environment Info\n";
print join("\n\t", "PATHs:", split(':', $ENV{PATH})), "\n";
print join("\n\t", "PERL5LIBs:", split(':', $ENV{PERL5LIB})), "\n";
print "\n";

my ($perl_version, $model_subname) = $test_spec =~ /(.*?)-(.*)/;
if ($perl_version ne Revision->perl_version()) {
    printf("Perl version specified by TEST_SPEC (%s) does not matching running version (%S)!\n", $perl_version, Revision->perl_version()) and exit(255);
}

my $model_name = sprintf('apipe-test-%s', $model_subname);
my $model = Genome::Model->get(name => $model_name);
if ($model) {
    printf("Found model (%s) with name '%s'.\n", $model->id, $model->name);
} else {
    printf("Could not get test model by name (%S)!\n", $model_name) and exit(255);
}

my $timeout = 7200;
my $test_version = Revision->test_version();

# Per Model Customizations
if (grep { $model_subname eq $_ } (
        'somatic-variation',
        'somatic-short',
        'somatic-validation',
        'somatic-variation-short',
        'somatic-variation-sv-detection',
        )
) {
    print("Customizing test environment...\n");
    print("Set GENOME_SOFTWARE_RESULT_TEST_NAME to 'jenkins-$job_name-$build_number'.\n");
    $ENV{GENOME_SOFTWARE_RESULT_TEST_NAME} = $test_version;
} else {
    print "No customizations for $model_name.\n";
}
print "\n";

my $build = Genome::Model::Build->get(
    model_name => $model->name,
    run_by => 'apipe-tester',
    software_revision => $test_version,
    status => ['Scheduled', 'Running', 'Succeeded'],
);
if ($build) {
    printf("Got existing build (%s) for current test version.\n", $build->__display_name__);
} else {
    $build = Genome::Model::Build->create(
        model_id => $model->id,
        software_revision => $test_version,
    );
    unless ($build) {
        print("Could not create new build!\n") and exit(255);
    }
    unless ($build->start()) {
        print("Cound not start new build!\n") and exit(255);
    }
    unless (UR::Context->commit()) {
        print("Could not save new build!\n") and exit(255);
    }
}

my $event = $build->the_master_event;
unless ($event) {
    print("Could not get the build's master event!\n") and exit(255);
}

printf("Monitoring build (%s) until it completes or timeout of %s minutes is reached.\n\n", $build->id, $timeout / 60);
my $interval = ($timeout > 30 ? 30 : 1);
my $start_time = time;
while (!grep { $event->event_status eq $_ } ('Succeeded', 'Failed', 'Crashed')) {
    UR::Context->current->reload($event);
    my $elapsed_time = time - $start_time;
    if ($elapsed_time > $timeout) {
        print("Timeout reached.\n");
        last;
    }
    sleep($interval);
}

$build = UR::Context->current->reload('Genome::Model::Build', id => $build->id);
if ($build->status eq 'Succeeded') {
    printf("Build status is %s.\n", $build->status);
} else {
    printf("Build is %s, not Succeeded!\n", $build->status) and exit(255);
}

my $list_cmd = sprintf('list-blessed-builds -m %s -p %s', $model->id, $perl_version);
my $list_cmd_output = qx($list_cmd);
unless ($list_cmd_output) {
    printf("No output from list command: %s!\n", $list_cmd) and exit(255);
}
chomp $list_cmd_output;
my $blessed_git_revision = (split("\t", $list_cmd_output))[2];
unless ($blessed_git_revision) {
    printf("No blessed git revision parsed from list command output:\n%s\n", $list_cmd_output) and exit(255);
}
my $blessed_snapshot_version = sprintf('%s-%s', $perl_version, $blessed_git_revision);

my $blessed_build = Genome::Model::Build->get(
    model_name => $model_name,
    run_by => 'apipe-tester',
    software_revision => $blessed_snapshot_version,
    status => 'Succeeded',
);
unless ($blessed_build) {
    printf("Could not get blessed build (%s)!\n", $blessed_snapshot_version) and exit(255);
}

my $new_build = Genome::Model::Build->get(
    model_name => $model_name,
    run_by => 'apipe-tester',
    software_revision => $test_version,
    status => 'Succeeded',
);
unless ($new_build) {
    print("Could not get test build!\n") and exit(255);
}

printf('Starting diff (blessed build = %s and new build = %s)...', $blessed_build->id, $new_build->id);
my $diff_cmd = Genome::Model::Build::Command::Diff->create(
    blessed_build => $blessed_build,
    new_build => $new_build,
);
unless ($diff_cmd->execute) {
    print("Diff command failed to execute!\n") and exit(255);
}

my $set_blessed_build_path = qx(which set-blessed-build);
unless ($set_blessed_build_path) {
    print("Unable to find set-blessed-build command!\n") and exit(255);
}
chomp $set_blessed_build_path;
my $set_cmd = sprintf('%s -m %s -p %s -g %s', $set_blessed_build_path, $model->id, Revision->perl_version(), Revision->git_revision());

my $has_diffs = (defined($diff_cmd->_diffs) && scalar(keys %{$diff_cmd->_diffs})) || 0;
if (!$has_diffs) {
    print qq($set_cmd\n);
    my $set_cmd_exit = system($set_cmd);
} else {
    print qq(Diffs found, sending notification email.\n);
    printf('If you want to bless this build run `%s`.', $set_cmd),

    my $build_type = get_model_camel_type($model);
    my @to = (Users->can($build_type)) ? Users->$build_type : Users::apipe();
    my @cc = Users::apipe();

    my $subject = sprintf('%s - Build %d - Diffs Found', $test_spec, $build_number);
    my $body = join("\n",
        sprintf('Project: %s', $job_name),
        sprintf('Build: %s', $build_url),
        sprintf('Console: %sconsole', $build_url),
        '********************************************************************************',
        sprintf('If you want to bless this build run `%s`.', $set_cmd),
        '********************************************************************************',
        $diff_cmd->diffs_message,
    );

    Library::send_mail(
        from => 'apipe-tester@genome.wustl.edu',
        to => Library::users_to_addresses(@to),
        cc => Library::users_to_addresses(@cc),
        subject => $subject,
        body => $body,
    );

    exit(255);
}

sub get_model_camel_type {
    my ($model) = shift;
    my $type = $model->class;
    $type =~ s/Genome::Model:://;
    return Genome::Utility::Text::camel_case_to_string($type, '_');
}
